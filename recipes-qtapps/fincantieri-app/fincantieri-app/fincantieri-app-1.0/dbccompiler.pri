DBC = $$files(*.dbc, false)
FIRSTDBC = $$first(DBC)
message(DBC=$$DBC FDBC=$$FIRSTDBC)

linux: MOC=../recipe-sysroot-native/usr/bin/qt5/moc
win32: MOC=moc.exe
linux: CANTOOLS=/usr/bin/cantools
win32: CANTOOLS=cantools

CONFIG(debug, debug|release) {
    DESTDIR = debug
} else {
    DESTDIR = release
}

INCLUDEPATH += $$DESTDIR

###########################################

extraX.name = "============= (AT LEAST ONE $DBC) to mycan.cpp mycan.h ============="
extraX.input = FIRSTDBC #  consider only first dbc (generate only once)
extraX.output  = mycan.h mycan.cpp
extraX.commands = $$CANTOOLS generate_qt_can_source # -o $$DESTDIR
extraX.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extraX

###########################################

extraX2.name = "============= (AT LEAST ONE $DBC) to mymodbus.cpp mymodbus.h ============="
extraX2.input = FIRSTDBC #  consider only first dbc (generate only once)
extraX2.output  = mymodbus.h mymodbus.cpp
extraX2.commands = $$CANTOOLS generate_qt_modbus_source # -o $$DESTDIR
extraX2.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extraX2

###########################################

extraY.name = "============= (AT LEAST ONE $DBC) to myabstractitemmodel.cpp myabstractitemmodel.h ============="
extraY.input = FIRSTDBC #  consider only first dbc (generate only once)
extraY.output  = myabstractitemmodel.h
extraY.commands = $$CANTOOLS generate_qt_model_source
extraY.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extraY

extraY2.name = "============= fake ============="
extraY2.input = FIRSTDBC
extraY2.output  = myabstractitemmodel.cpp
extraY2.commands = type nul >> ${QMAKE_FILE_OUT}
extraY2.CONFIG = no_link target_predeps
extraY2.depends = myabstractitemmodel.h
QMAKE_EXTRA_COMPILERS += extraY2

###########################################

extra1.name = "============= EVERY $DBC to <file>.c <file>.h ============="
extra1.input = DBC
extra1.output  = ${QMAKE_FILE_BASE}.c ${QMAKE_FILE_BASE}.h
extra1.commands = $$CANTOOLS generate_c_source --bit-fields --no-range-check --no-size-and-memset --no-strict ${QMAKE_FILE_IN}
extra1.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extra1

###########################################

extra2.name = "============= EVERY $DBC to <file>_qt.cpp <file>_qt.h ============="
extra2.input = DBC
extra2.output  = ${QMAKE_FILE_BASE}_qt.h ${QMAKE_FILE_BASE}_qt.cpp # ${QMAKE_FILE_BASE}_qt_init.cpp
extra2.commands = $$CANTOOLS generate_qt_source ${QMAKE_FILE_IN} --signals all --bit-fields --no-strict --no-size-and-memset
extra2.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extra2

post2.name = "============= EVERY $DBC to <file>_qt_init.cpp ============="
post2.input = DBC
post2.output  = ${QMAKE_FILE_BASE}_qt_init.cpp
post2.commands = type nul >> ${QMAKE_FILE_OUT}
post2.CONFIG = no_link target_predeps
post2.depends = ${QMAKE_FILE_BASE}_qt.h
QMAKE_EXTRA_COMPILERS += post2

###########################################

MOCABLE = mycan.h mymodbus.h myabstractitemmodel.h $$replace(DBC, ".dbc", "_qt.h")

###########################################

extra3.name = "============= MOC of header autogenerated cantools file ============="
extra3.input = MOCABLE
extra3.output = moc_${QMAKE_FILE_BASE}.cpp
extra3.commands = if [ ${QMAKE_FILE_EXT} = .h ]; then $$MOC $(DEFINES) $(INCLUDEPATH) ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}; fi
extra3.CONFIG = no_link target_predeps
QMAKE_EXTRA_COMPILERS += extra3

###########################################

DATABASES=$$replace(DBC, ".dbc", "")
MYMOCS=$$join(DATABASES, '_qt.cpp moc_', moc_, _qt.cpp)
MYMOCS2=$$split(MYMOCS, " ")

GENERATED_SOURCES = mycan.cpp mymodbus.cpp moc_mymodbus.cpp moc_mycan.cpp myabstractitemmodel.cpp moc_myabstractitemmodel.cpp $$replace(DBC, ".dbc", ".c") $$replace(DBC, ".dbc", "_qt.cpp") $$replace(DBC, ".dbc", "_qt_init.cpp") $$MYMOCS2
HEADERS = $$replace(DBC, ".dbc", ".h") $$replace(DBC, ".dbc", "_qt.h")

# system( echo 2 1>&2 )

